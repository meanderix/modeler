/*
FCC lattice model of the atomic nucleus
Inspired by the Nuclear Visualization Software by Norman D. Cook.

Copyright (C) 2019 Mattias Andersson. All rights reserved.
*/
@preset helium_4 { protons: 2, neutrons: 2 }
@preset oxygen_16 { protons: 8, neutrons: 8 }
@preset calcium_40 { protons: 20, neutrons: 20 }
@preset nickel_56 { protons: 28, neutrons: 28 }
@preset zirconium_80 { protons: 40, neutrons: 40 }
@preset tin_100 { protons: 50, neutrons: 50 }
@preset ytterbium_140 { protons: 70, neutrons: 70 }
@preset lead_164 { protons: 82, neutrons: 82 }
@preset copernicium_224 { protons: 112, neutrons: 112 }
@preset unbihexium_252 { protons: 126, neutrons: 126 }

@param int protons 2 0 150
@param int neutrons 2 0 150
@param float particle_size 0.5 0 1

@param enum colouring isospin spin total_spin principal magnetic_spin

@param bool X_AXIS 1
@param bool Y_AXIS 1
@param bool Z_AXIS 1

@param bool LATTICE_1 0
@param bool LATTICE_2 0
@param float lattice_size 0.12 0 0.25

background 1 1 1
startshape START
duration 60
hashsize 2048
cellsize 1
minsize -1

linerule LINE
camerarule CAMERA

evalmode bfs
wrapmode none
center


isosurface SPHERE {
  xyz -1 1 6
  function {1.0 - (x*x+y*y+z*z)}
}

rule CAMERA {
  FRAME{rx -90 z -3.5}
  CAMERARULE {}
}
rule LINE {
  8/8*[rz 360] VERTEX{x $lattice_size G 0.3 R 0.3 B 0.3}
}

selector PARTICLE_HASH autohash

selector PROTON sequential
selector NEUTRON sequential
selector _PARTICLE weight u

selector RECURSE depth
selector RECURSE_XY depth
selector RECURSE_Z hash
selector TETRAHEDRA autohash

rule BALL {
  SPHERE{R 1.1 s $particle_size}
}

rule PROTON $protons {
  BALL{}
  EDGES{}
}

rule NEUTRON $neutrons {
  BALL{
@IFDEF isospin
    H 0.6
@ENDIF
  }
  EDGES{}
}

rule _PARTICLE 0 {
  PROTON{}
}
rule _PARTICLE 1 {
  NEUTRON{}
}

rule PARTICLE_HASH 0 {
  _PARTICLE{}
}

rule EDGES {
  _EDGES{x -1 -1 -1 R 0.5 G 0.5 B 0.5}
}

rule _EDGES {
@IFDEF LATTICE_1
  MOVETO {x  1  1  1}
  SLINETO{x -1 -1  1}
  SLINETO{x  1 -1 -1}
  SLINETO{x -1  1 -1}
  SLINETO{x  1  1  1}  
  SLINETO{x  1 -1 -1}  
  MOVETO {x -1 -1  1}
  SLINETO{x -1  1 -1}
@ENDIF
@IFDEF LATTICE_2
  MOVETO{x  1  1  1} SLINETO{}
  MOVETO{x  1 -1 -1} SLINETO{}
  MOVETO{x -1  1 -1} SLINETO{}
  MOVETO{x -1 -1  1} SLINETO{}  
@ENDIF
}

rule PARTICLE {
  PARTICLE_HASH{x 1 1 1}
}

rule RECURSE_Z 0 {
  RECURSE{
@IFDEF principal
    H 0.15
@ENDIF
  }
}

rule RECURSE_XY 14 {
  TETRAHEDRA{
@IFDEF principal
    H 0.15
@ENDIF
@IFDEF total_spin
    H 0.15
@ENDIF
  }
}

rule RECURSE 13 {
  RECURSE_XY{}
  RECURSE_Z{z -2 fz}
  RECURSE_Z{z 2 fz}
}

rule TETRAHEDRA 0 {
  // spin up proton
  PARTICLE{}
  // spin down proton
  PARTICLE{fx fy
@IFDEF spin
    H 0.4
@ENDIF
  }
  // spin up neutron
  PARTICLE{fx fz u 1}
  // spin down neutron
  PARTICLE{fy fz u 1
@IFDEF spin
    H 0.4
@ENDIF
  }

  RECURSE_XY{x 2 fx
@IFDEF magnetic_spin
    H 0.15
@ENDIF
  }
  RECURSE_XY{x -2 fx
@IFDEF magnetic_spin
    H 0.15
@ENDIF
  }
  RECURSE_XY{y 2 fy}
  RECURSE_XY{y -2 fy}
}

rule DRAWAXIS {
  MOVETO{z -13 s 0}
  LINETO{z -13}
  LINETO{z 12}
  LINETO{z 12 s 2.3}
  LINETO{z 13.2 s 0}  
}
rule DRAWAXES {
@IFDEF X_AXIS
  DRAWAXIS{ry 90 R 1}
@ENDIF
@IFDEF Y_AXIS
  DRAWAXIS{rz 90 ry 90 G 1}
@ENDIF
@IFDEF Z_AXIS
  DRAWAXIS{B 1}
@ENDIF
}

rule TRANSFORM {
  FRAME{}
  TRANSFORM{rz 1}
}
rule START : TRANSFORM {
  RECURSE{}
  DRAWAXES{}
}
